CONTEXT Employment IN ENGLISH
PURPOSE CONTEXT Employment
{+ This context looks at the employment of people within 'Our Organization'.
   This means that we do not consider anything outside of 'Our Organization'
+}

--[Definitions for Concepts]

CONCEPT Person     "a human being"
CONCEPT Department "a functional unit within an organization that is being managed."
CONCEPT Function   "a job position or title that employees can hold."

--[Definitions for Relations]

RELATION boardMember[Person] [PROP]        MEANING "{{SRC}} is a member of the board of our organization"
RELATION oversees[Person*Department]       MEANING "{{SRC}} oversees department {{TGT}}"

RELATION deptManager[Department*Person]    MEANING "{{SRC}} has {{TGT}} as its manager"
RELATION deptFunction[Department*Function] MEANING "{{SRC}} has job positions for employees that can fulfill {{TGT}}"

RELATION isQualifiedFor[Person*Function]   MEANING "{{SRC}} has been qualified to fulfill funtion {{TGT}}"
RELATION employee[Person] [PROP]           MEANING "{{SRC}} has a working relationship/contract with our organization"
RELATION emplDepartment[Person*Department] MEANING "{{SRC}} works in department {{TGT}} of our organization"
RELATION emplFunction[Person*Function]     MEANING "{{SRC}} has been assigned the function {{TGT}}"
RELATION reportsTo[Person*Person]          MEANING "{{SRC}} reports to {{TGT}}"

--[Principles: Board and organization structure]

PURPOSE RULE "B1: The board must oversee all departments"
{+In order to be in control, every deparment must be overseen by a member of the board - no exceptions.+}

PURPOSE RULE "B2: Only department managers report to board members"
{+In order to ensure the workload of board members is limited, only department managers may report to the board.+}

PURPOSE RULE "B3: Only the managers of departments that are overseen by a particular board member may, and must report to that board member."
{+Proper reporting to the board means that the board members that oversee a department will be reported to by its manager.
  Also, each such manager must report to the board member that oversees its department.+}

--[Principles: Departments]

PURPOSE RULE "D1: Departments must have precisely one manager"
{+Departments must be led/managed. The responsibility for doing so must be assigned to a single person,
to ensure that it is always unambiguously clear who it is that bears that responsibility.+}

PURPOSE RULE "D2: Departments must list the functions for which they have positions"
{+Hiring people that can do the work in a department (a functional unit) becomes easier if sets of
  capabilities are specified (Functions) that the deparment needs to get its work done. 
  Therefore, departments should list the functions for which they have job positions.+}

PURPOSE RULE "D3: Departments must have employees"
{+A department that doesn't have employees cannot do work.+}

PURPOSE RULE "D4: Everyone that works for a department must be an employee (have a contract)"
{+In order to make sure that people working in one of our organization's departments are well-behaved,
  they must be employees, which implies they have signed a contract that provides appropriate guarantees for that.+}

--[Principles: Employees]

PURPOSE RULE "E1: All employees must have one function"
{+In order to not overload employees, it has been decided that they can have at most one function.+}

PURPOSE RULE "E2 An employee that has been assigned a function must have the associated qualifications"
{+People should only be employed to fulfill a function if they can do the associated work.
  Therefore, it must have been established that they can do the work if they are assigned a function.+}

PURPOSE RULE "E3: All employees must be assigned to a department for which their function is relevant"
{+In order to guarantee that people end up in a department that can use the capabilities 
  for which they are qualified, people should only be assigned to deparments that can use those.+}

--[Principles: Reporting]

PURPOSE RULE "R1: People must not report to themselves"
{+Butchers should not judge their own meat, and similarly, people must not report to themselves.+}

-- PURPOSE RULE "R2: Employees must report to their manager"
-- {+In order not to overload people with red tape, people must report to at most one person.+}

PURPOSE RULE "R2: All employees that are not the department manager must report to their department manager"
{+The easiest reporting line for employees is to report to their department manager (provided they are not the manager themselves).+}

-- PURPOSE RULE "R3: Managers must report to a member of the board"
-- {+To ensure that the board can oversee all departments, each manager must report to some member of the board.+}

PURPOSE RULE "R3: Managers must report to the board member that oversees his department"
{+Since the board has decided to assign departments to its members to oversee, it makes sense 
  to have the managers of these departments report to the board member that oversees his/her department.+}

------------------------------------------------
--[FORMALIZING THE PRINCIPLES (USING AMPERSAND)]
------------------------------------------------
CONCEPT Criterion "a (human readable) criterion that, if accepted by our organization, must be complied with"
CONCEPT Principle "a text that identifies a particular formalization (rule) of a Criterion"

RELATION pAccepted[Principle] [PROP] MEANING "The rule with ID {{TGT}} has been accepted and therefor must be complied with"

RELATION pCrit[Principle*Criterion] [INJ,SUR,UNI,TOT] MEANING "{{SRC}} is an identifier for principle {{TGT}}"
POPULATION pCrit[Principle*Criterion] CONTAINS
[ ("B1", "The board must oversee all departments")
, ("B2", "Only department managers report to board members")
, ("B3", "Only the managers of departments that are overseen by a particular board member may, and must report to that board member.")
, ("D1", "Departments must have precisely one manager")
, ("D2", "Departments must list the functions for which they have positions")
, ("D3", "Departments must have employees")
, ("D4", "Everyone that works for a department must be an employee (have a contract)")
, ("E1", "All employees must have one function")
, ("E2", "An employee that has been assigned a function must have the associated qualifications")
, ("E3", "All employees must be assigned to a department for which their function is relevant")
, ("R1", "People must not report to themselves")
, ("R2", "All employees that are not the department manager must report to their department manager")
, ("R3", "Managers must report to the board member that oversees his department")
]

RELATION pAccepted[Principle] [PROP] MEANING "The rule with ID {{TGT}} has been accepted and therefor must be complied with"
-- POPULATION pAccepted[Principle*Principle] CONTAINS
-- [ ("B1","B1") 
-- , ("D1","D1"), ("D2","D2"), ("D3","D3"), ("D4","D4")
-- , ("E1","E1"), ("E2","E2"), ("E3","E3")
-- , ("R1","R1"), ("R2","R2"), ("R3","R3")
-- ]

--[Principles: Board and organization structure]

RULE "B1: The board must oversee all departments":
   V;pAccepted;"B1";V /\ I[Department] |- oversees~;boardMember;V

RULE "B2: Only department managers report to board members":
   V;pAccepted;"B2";V /\ reportsTo;boardMember |- deptManager~;V

RULE "B3: Only the managers of departments that are overseen by a particular board member may, and must report to that board member.":
   V;pAccepted;"B3";V /\ reportsTo;boardMember = V;pAccepted;"B3";V /\ (oversees;deptManager)~

--[Principles: Departments]

RULE "D1: Departments must have precisely one manager":
   V;pAccepted;"D1";V /\ I[Department] |- deptManager;deptManager~

RULE "D2: Departments must list the functions for which they have positions":
   V;pAccepted;"D2";V /\ deptFunction |- deptFunction;-I 
MEANING "Each department must have at least two different functions specified. It is assumed that one of them is `Manager`."

RULE "D3: Departments must have employees":
   V;pAccepted;"D3";V /\ I[Department] |- emplDepartment~;emplDepartment

RULE "D4: Everyone that works for a department must be an employee (have a contract)":
   V;pAccepted;"D4";V /\ emplDepartment |- employee;emplDepartment

--[Principles: Employees]

RULE "E1: All employees must have one function":
   V;pAccepted;"E1";V /\ employee |- emplFunction;emplFunction~ /\ -(emplFunction;-I;emplFunction~)

RULE "E2 An employee that has been assigned a function must have the associated qualifications":
   V;pAccepted;"E2";V /\ emplFunction |- isQualifiedFor

RULE "E3: All employees must be assigned to a department for which their function is relevant":
   V;pAccepted;"E3";V /\ emplDepartment~;emplFunction |- deptFunction

--[Principles: Reporting]

RULE "R1: People must not report to themselves":
   V;pAccepted;"R1";V /\ reportsTo |- -I[Person]

-- RULE "R2: Employees must report to their manager":
--    emplDepartment;deptManager |- reportsTo

RULE "R2: All employees that are not the department manager must report to their department manager":
   V;pAccepted;"R2";V /\ (emplDepartment-deptManager~);deptManager |- reportsTo

-- RULE "R3: Managers must report to a member of the board":
--    deptManager~ |- reportsTo;boardMember;V

RULE "R3: Managers must report to the board member that oversees his department":
   V;pAccepted;"R3";V /\ (oversees;deptManager)~ |- reportsTo

-----------------------------------------
--[DEFINING INTERFACES FOR THE PROTOTYPE]
-----------------------------------------

INTERFACE "Board": V[ONE*Person];boardMember
BOX <TABLE>
   [ "Board member" : I[Person]
   , "Oversees" : oversees
   , "Rep. Mgrs." : reportsTo~
   , "Qualified for": isQualifiedFor
   ]

INTERFACE "Departments": V[ONE*Department] 
BOX <TABLE>
   [ "Department" : I[Department]
   , "Manager" : deptManager
   , "Employees" : emplDepartment~
   , "Functions" : deptFunction
   ]

INTERFACE "Functions": V[ONE*Function] 
BOX <TABLE>
   [ "Function" : I[Function]
   , "Departments" : deptFunction~
   , "Employees" : emplFunction~
   ]

INTERFACE "Employees": V[ONE*Person];employee 
BOX <TABLE>
   [ "Name" : I[Person]
   , "Department" : emplDepartment
   , "Function" : emplFunction
   , "Reports to": reportsTo
   , "Qualified for": isQualifiedFor
   ]

INTERFACE "Other people": V[ONE*Person];employee 
BOX <TABLE>
   [ "Name" : I[Person]
   , "Board member?" : boardMember
   , "Function" : emplFunction
   , "Reports to": reportsTo 
   , "Qualified for": isQualifiedFor
   ]

INTERFACE "Principle Acceptance": V[ONE*Principle] cRud
BOX<TABLE>
   [ "Accept?": pAccepted
   , "ID": I[Principle] cRud
   , "Criterion": pCrit cRud
   ]

INTERFACE "My Organization": "_SESSION";V[SESSION*ONE]
BOX <TABS>
   [ "The Board":            I INTERFACE "Board"
   , "Departments":          I INTERFACE "Departments"
   , "Functions":            I INTERFACE "Functions"
   , "Employees":            I INTERFACE "Employees"
   , "Other people":         I INTERFACE "Other people"
   , "Principle Acceptance": I INTERFACE "Principle Acceptance"
   ]

------------------------------------------------
--[SPECIFYING A TEST POPULATION]
------------------------------------------------

--[The Board]

POPULATION boardMember[Person*Person] CONTAINS
   [ ("Boris", "Boris"), ("Birgit", "Birgit"), ("Bram", "Bram") ]

POPULATION oversees[Person*Department] CONTAINS
   [ ("Boris", "Finance")
   , ("Birgit", "Operations")
   , ("Bram", "Support") 
   ]

--[Departments]

POPULATION deptManager[Department*Person] CONTAINS
   [ ("Finance","Alice")
   , ("Operations","Darryl")
   , ("Support","George")
   ]

POPULATION deptFunction[Department*Function] CONTAINS
   [ ("Finance","Manager"), ("Finance","Controller"), ("Finance","Administrator")
   , ("Operations","Manager"), ("Operations","Operator")
   , ("Support","Manager"), ("Support","Support engineer")
   ]

--[Employees]

POPULATION employee[Person*Person] CONTAINS
   [ ("Alice","Alice")
   , ("Bob","Bob")
   , ("Charlie","Charlie")
   , ("Darryl","Darryl") 
   , ("Evert","Evert")
   , ("Flora","Flora")
   , ("George","George")
   , ("Harry","Harry")
   ]

POPULATION emplDepartment[Person*Department] CONTAINS
   [ ("Alice","Finance")
   , ("Bob","Finance")
   , ("Charlie","Finance")
   , ("Darryl","Operations")
   , ("Evert","Operations")
   , ("Flora","Operations")
   , ("George","Support")
   , ("Harry","Support")
   ]

POPULATION isQualifiedFor[Person*Function] CONTAINS
   [ ("Alice","Manager"), ("Alice","Controller"), ("Alice","Administrator")
   , ("Bob","Controller"), ("Bob","Administrator")
   , ("Charlie","Administrator")
   , ("Darryl","Manager"), ("Darryl","Operator")
   , ("Evert","Operator")
   , ("Flora", "Operator")
   , ("George","Manager"), ("George","Controller")
   , ("Harry","Support engineer"), ("Harry","Operator")
   , ("Irene","Manager"), ("Irene","Controller")
   , ("John","Operator"), ("John","Support Engineer")
   , ("Karel","CISO"), ("Karel","Security Officer")
   ]

POPULATION emplFunction[Person*Function] CONTAINS
   [ ("Alice","Manager")
   , ("Bob","Controller")
   , ("Charlie","Administrator")
   , ("Darryl","Manager")
   , ("Evert","Operator")
   , ("Flora", "Operator")
   , ("George","Manager")
   , ("Harry","Support engineer")
   ]

POPULATION reportsTo[Person*Person] CONTAINS
   [ ("Alice","Boris")
   , ("Bob","Alice")
   , ("Charlie","Alice")
   , ("Darryl","Birgit")
   , ("Evert","Darryl")
   , ("Flora", "Darryl")
   , ("George","Bram")
   , ("Harry","George")
   ]

ENDCONTEXT